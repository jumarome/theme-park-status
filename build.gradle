plugins {
    id 'application'
    id 'org.unbroken-dome.test-sets' version '4.0.0'
}

testSets {
    integrationTest
}

application{
    mainClass = 'com.gradlehero.themepark.RideStatusService'
}

tasks.named('check'){
    dependsOn tasks.named('integrationTest')
}
tasks.register('runJar',JavaExec){
    classpath tasks.named('jar').map{it.outputs}
    classpath configurations.runtimeClasspath
    args ' teacups'
    mainClass = 'com.gradlehero.themepark.RideStatusService'
}

tasks.named('jar') {
    manifest {
        attributes('Main-Class': 'com.gradlehero.themepark.RideStatusService')
    }
}

repositories {
    mavenCentral()
}

ext {
    junitJupiterVersion = '5.7.2'
}

java{
    toolchain{
        languageVersion = JavaLanguageVersion.of(11)
    }
}


dependencies {
    implementation 'org.apache.commons:commons-lang3:3.11'
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitJupiterVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-params:$junitJupiterVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitJupiterVersion"
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
}
